// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'org.jetbrains.dokka'

buildscript {
    ext {
        compileSdk = 34
        minSdk = 21
        targetSdk = 34

        versionCode = properties['VERSION_CODE'].toInteger()
        versionName = "${properties['VERSION_NAME']}"

        // Software versions
        kotlinVersion = '1.8.0'
        dokkaVersion = "1.8.20"
        exoplayerVersion = '1.1.1'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

tasks.dokkaHtmlMultiModule {
    moduleName.set("api.video Android $project.name library")
    suppressInheritedMembers.set(true)

    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ 
                "customAssets" : [
                    "${file("$rootDir/docs/docs_logo.svg")}",
                    "${file("$rootDir/docs/logo-icon.svg")}"
                ] 
        }"""]
    )
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}